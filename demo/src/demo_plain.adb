--  Plain Ada version of the demo.
--  In Compiler: Runtime_Dep := Runtime_Plain_Ada.

with Ada.Streams.Stream_IO;
with Ada.Strings.Unbounded;
with Ada.Text_IO;

with My_Format.Basics;
     --  ^ Generated by "protoc --ada_out=. basics.proto" (cf build_demo.cmd).

procedure Demo_Plain is

   procedure Show (p : My_Format.Basics.Person) is
      use Ada.Strings.Unbounded, Ada.Text_IO;
   begin
      Put_Line
         (To_String (p.Name) &
          ", " &
          (if p.Gender.Is_Set then
              p.Gender.Value'Image
           else
              "Unknown gender") &
          ',' &
          p.Birth_Year'Image);
    end Show;

   procedure Write_Person (db_name : String) is
      use Ada.Streams.Stream_IO, Ada.Strings.Unbounded, My_Format.Basics;
      f : File_Type;
   begin
      Create (f, Out_File, db_name);
      Person'Write
         (Stream (f),
            ((Name       => To_Unbounded_String ("Plinius"),
              Birth_Year => 23,
              Gender     => (Is_Set => True, Value => Male))));
      Close (f);
   end Write_Person;

   procedure Read_Person (db_name : String) is
      use Ada.Streams.Stream_IO, My_Format.Basics;
      p : Person;
      f : File_Type;
   begin
      Open (f, In_File, db_name);
      Person'Read (Stream (f), p);
      Close (f);

      Show (p);
   end Read_Person;

   procedure Write_Crowd (db_name : String) is
      use Ada.Streams.Stream_IO, Ada.Strings.Unbounded, My_Format.Basics;
      c : Crowd;
      f : File_Type;
   begin
      Create (f, Out_File, db_name);

      c.Individual.Append
         ((Name       => To_Unbounded_String ("Salvador"),
           Birth_Year => 1904,
           Gender     => (Is_Set => True, Value => Male)));

      c.Individual.Append
         ((Name       => To_Unbounded_String ("Ada"),
           Birth_Year => 1815,
           Gender     => (Is_Set => True, Value => Female)));

      c.Individual.Append
         ((Name       => To_Unbounded_String ("XYAZ_13"),
           Birth_Year => 2010,
           Gender     => (Is_Set => False)));

      --  Testing Varint's:
      c.Useless_Symbol_1 :=             1;
      c.Useless_Symbol_2 :=         1_111;
      c.Useless_Symbol_3 :=     1_111_111;
      c.Useless_Symbol_4 := 1_111_111_111;
      c.Useless_Symbol_5 := 2_020_202_020;
      c.Useless_Symbol_6 :=             2;
      c.Useless_Symbol_7 :=     2_020_202;
      c.Useless_Symbol_8 :=         2_020;

      Crowd'Write (Stream (f), c);

      Close (f);
   end Write_Crowd;

   procedure Read_Crowd (db_name : String) is
      use Ada.Streams.Stream_IO, My_Format.Basics;
      c : Crowd;
      f : File_Type;
   begin
      Open (f, In_File, db_name);
      Crowd'Read (Stream (f), c);
      Close (f);

      for i in 1 .. Length (c.Individual) loop
        Show (c.Individual (i));
      end loop;

      --  Testing Varint's:
      Ada.Text_IO.Put_Line ("Useless_Symbol 1 =" & c.Useless_Symbol_1'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 2 =" & c.Useless_Symbol_2'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 3 =" & c.Useless_Symbol_3'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 4 =" & c.Useless_Symbol_4'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 5 =" & c.Useless_Symbol_5'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 6 =" & c.Useless_Symbol_6'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 7 =" & c.Useless_Symbol_7'Image);
      Ada.Text_IO.Put_Line ("Useless_Symbol 8 =" & c.Useless_Symbol_8'Image);

   end Read_Crowd;

begin
  --  Just a person.
  Write_Person ("person.pb");
  Read_Person  ("person.pb");

  --  A crowd.
  Write_Crowd ("crowd.pb");
  Read_Crowd  ("crowd.pb");
end Demo_Plain;


